packer {
  required_plugins {
    proxmox = {
      version = ">= 1.2.2"
      source  = "github.com/hashicorp/proxmox"
    }
  }
}

variable "insecure" {
  type    = bool
  default = true
}

variable "pve_endpoint" {
  type    = string
  default = "192.168.20.11:8006"
}

variable "node" {
  type    = string
  default = "hyv-pve-01"
}

variable "vm_name" {
  type    = string
  default = "ubuntu-24-base"
}

variable "vm_id" {
  type    = number
  default = null
}

variable "tags" {
  type    = string
  default = "packer"
}

variable "memory" {
  type    = number
  default = 2048
}

variable "cores" {
  type    = number
  default = 2
}

variable "cpu_type" {
  type    = string
  default = "host"
}

variable "qemu_agent" {
  type    = bool
  default = true
}

variable "cloud_init" {
  type    = bool
  default = true
}

variable "ssh_public_key_file" {
  type    = string
  default = "~/.ssh/id_ed25519.pub"
}

variable "ssh_private_key_file" {
  type    = string
  default = "~/.ssh/id_ed25519"
}

variable "iso_file" {
  type    = string
  default = "local:iso/ubuntu-24.04.3-live-server-amd64.iso"
}

locals {
  vm_name              = format("template-%s", var.vm_name)
  proxmox_url          = format("https://%s/api2/json", var.pve_endpoint)
  template_description = format("%s generated by Packer %s", var.vm_name, formatdate("DD MMM YYYY hh:mm ZZZ", timestamp()))
  http_content = {
    "/user-data" = templatefile("config/user-data.pkrtpl.hcl", {
      ssh_key = trimspace(file(pathexpand(var.ssh_public_key_file)))
    })
    "/meta-data" = "" # Ubuntu autoinstall requires this file to exist, even if empty
  }
}

source "proxmox-iso" "ubuntu" {
  insecure_skip_tls_verify = var.insecure
  proxmox_url              = local.proxmox_url
  node                     = var.node
  vm_name                  = local.vm_name
  vm_id                    = var.vm_id
  tags                     = var.tags
  memory                   = var.memory
  cores                    = var.cores
  cpu_type                 = var.cpu_type
  qemu_agent               = var.qemu_agent
  cloud_init               = var.cloud_init
  cloud_init_storage_pool  = "local-lvm"
  template_description     = local.template_description

  ssh_username         = "packer"
  ssh_private_key_file = var.ssh_private_key_file

  scsi_controller = "virtio-scsi-pci"

  boot_iso {
    iso_file = var.iso_file
    unmount  = true
  }
  disks {
    disk_size    = "16G"
    storage_pool = "local-lvm"
    type         = "scsi"
  }
  efi_config {
    efi_storage_pool  = "local-lvm"
    efi_type          = "4m"
    pre_enrolled_keys = true
  }
  network_adapters {
    bridge = "vmbr0"
    model  = "virtio"
  }

  # Slow down the typing so GRUB doesn't drop characters
  boot_keygroup_interval = "100ms"
  boot_wait              = "10s"
  ssh_timeout            = "20m"
  http_content           = local.http_content
  boot_command = [
    "<wait5s>",
    "c<wait2s>",
    # Load Kernel with the Autoinstall Flags
    "linux /casper/vmlinuz --- autoinstall quiet 'ds=nocloud-net;s=http://{{ .HTTPIP }}:{{ .HTTPPort }}/'<enter><wait2s>",
    "initrd /casper/initrd<enter><wait2s>",
    "boot<enter>"
  ]
}

build {
  sources = [
    "source.proxmox-iso.ubuntu"
  ]
  provisioner "shell" {
    scripts = fileset(".", "scripts/clean.sh")
  }
}
